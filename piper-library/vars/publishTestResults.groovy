import com.sap.piper.internal.JenkinsUtils
import com.sap.icd.jenkins.Utils
import com.sap.piper.internal.ConfigurationHelper
import com.sap.piper.internal.MapUtils
import com.sap.piper.internal.Notify

import groovy.transform.Field

import static com.sap.piper.internal.Prerequisites.checkScript

@Field Set TOOLS = ['cobertura','contiperf','gauge','html','jacoco','jmeter','junit','lcov','supa','supt']

@Field String STEP_NAME = 'publishTestResults'
@Field Set GENERAL_CONFIG_KEYS = TOOLS
@Field Set STEP_CONFIG_KEYS = GENERAL_CONFIG_KEYS.plus([
    'failOnError'
])
@Field Set PARAMETER_KEYS = STEP_CONFIG_KEYS

void call(Map parameters = [:]) {
    handlePipelineStepErrors (stepName: STEP_NAME, stepParameters: parameters,
        libraryDocumentationUrl: 'https://go.sap.corp/piper/',
        libraryRepositoryUrl: 'https://github.wdf.sap.corp/ContinuousDelivery/piper-library/'
    ) {
        def script = checkScript(this, parameters) ?: this
        def utils = parameters.juStabUtils ?: new Utils()
        // notify about deprecated step usage
        Notify.deprecatedStep(this, "testsPublishResults", "removed", script?.commonPipelineEnvironment)
        // harmonize parameters
        prepare(parameters)
        // load default & individual configuration
        Map config = ConfigurationHelper
            .loadStepDefaults(this)
            .mixinGeneralConfig(script.globalPipelineEnvironment, GENERAL_CONFIG_KEYS)
            .mixinStepConfig(script.globalPipelineEnvironment, STEP_CONFIG_KEYS)
            .mixinStageConfig(script.globalPipelineEnvironment, parameters.stageName?:env.STAGE_NAME, STEP_CONFIG_KEYS)
            .mixin(parameters, PARAMETER_KEYS)
            .use()

        config.jmeterAnalysis.active = config.jmeter.active && config.jmeter.analysisReport
        config.jmeterDashboard.active = config.jmeter.active && config.jmeter.dashboardReport

        // UNIT TESTS
        publishJUnitReport(config.junit)
        // CODE COVERAGE
        publishJacocoReport(config.jacoco)
        publishCoberturaReport(config.cobertura)
        publishHtmlReport(config.lcov)
        // PERFORMANCE
        publishHtmlReport(config.contiperf)
        // publish Performance Report using "Jenkins Performance Plugin" https://wiki.jenkins.io/display/JENKINS/Performance+Plugin
        publishPerformanceReport(config.jmeter)
        // publish report generated by jmeter-analysis-maven-plugin https://github.com/afranken/jmeter-analysis-maven-plugin
        publishHtmlReport(config.jmeterAnalysis)
        // publish report generated by Jmeter build-in dashboard report https://github.com/jmeter-maven-plugin/jmeter-maven-plugin/issues/208
        publishHtmlReport(config.jmeterDashboard)
        publishHtmlReport(config.supa)
        publishHtmlReport(config.supt)
        // OTHERS
        publishHtmlReport(config.gauge)
        publishHtmlReport(config.html)

        if (config.failOnError && (JenkinsUtils.hasTestFailures(script.currentBuild) || 'FAILURE' == script.currentBuild?.result)){
            script.currentBuild.result = 'FAILURE'
            error "[${STEP_NAME}] Some tests failed!"
        }
    }
}

void publishJUnitReport(Map settings = [:]) {
    if(settings.active){
        if (settings.updateResults)
            touchFiles(settings.pattern)
        junit(
            testResults: settings.pattern,
            allowEmptyResults: settings.allowEmptyResults,
            healthScaleFactor: 100.0,
        )
        archiveResults(settings.archive, settings.pattern, settings.allowEmptyResults)
    }
}

void publishJacocoReport(Map settings = [:]) {
    if(settings.active){
        jacoco(
            classPattern: settings.classes,
            sourcePattern: settings.sources,
            execPattern: settings.pattern,
            inclusionPattern: settings.include,
            exclusionPattern: settings.exclude
        )
        archiveResults(settings.archive, settings.pattern, settings.allowEmptyResults)
    }
}

void publishCoberturaReport(Map settings = [:]) {
    if(settings.active){
        cobertura(
            coberturaReportFile: settings.pattern,
            onlyStable: settings.onlyStableBuilds,
            failNoReports: !settings.allowEmptyResults,
            failUnhealthy: false,
            failUnstable: false,
            autoUpdateHealth: false,
            autoUpdateStability: false,
            maxNumberOfBuilds: 0
        )
        archiveResults(settings.archive, settings.pattern, settings.allowEmptyResults)
    }
}

void publishPerformanceReport(Map settings = [:]){
    if(settings.active){
        // handle legacy parameters
        if(settings.failBuildIfNoResultFile != null)
            settings.allowEmptyResults = !settings.failBuildIfNoResultFile
        perfReport(
            sourceDataFiles: settings.pattern,
            errorFailedThreshold: settings.errorFailedThreshold,
            errorUnstableThreshold: settings.errorUnstableThreshold,
            errorUnstableResponseTimeThreshold: settings.errorUnstableResponseTimeThreshold,
            relativeFailedThresholdPositive: settings.relativeFailedThresholdPositive,
            relativeFailedThresholdNegative: settings.relativeFailedThresholdNegative,
            relativeUnstableThresholdPositive: settings.relativeUnstableThresholdPositive,
            relativeUnstableThresholdNegative: settings.relativeUnstableThresholdNegative,
            modePerformancePerTestCase: false,
            modeOfThreshold: settings.modeOfThreshold,
            modeThroughput: settings.modeThroughput,
            nthBuildNumber: settings.nthBuildNumber,
            configType: settings.configType,
            failBuildIfNoResultFile: !settings.allowEmptyResults,
            compareBuildPrevious: settings.compareBuildPrevious
        )
        archiveResults(settings.archive, settings.pattern, settings.allowEmptyResults)
    }
}

void publishHtmlReport(Map settings = [:]){
    if(settings.active){
        if (settings.path && !settings.path.endsWith('/')) settings.path += '/'
        def pattern = "${settings.path}${settings.file}"
        def resultFiles = findFiles(glob: pattern)

        echo "[${STEP_NAME}] found ${resultFiles.length} file(s) to publish for pattern '${pattern}'"
        //publishHTML does not support FileSet under multiple sub-folders. Issue https://github.wdf.sap.corp/IndustryCloudFoundation/continuous-delivery/issues/171
        for (File file : resultFiles) {
            def fileDir = "${file.path.replaceAll(file.name, '')}"
            publishHTML([
                allowMissing         : settings.allowEmptyResults,
                alwaysLinkToLastBuild: false,
                keepAll              : true,
                reportDir            : "${fileDir}",
                reportFiles          : "${file.name}",
                reportName           : "${settings.name} ${file.path.replace('/index.html', '').replaceAll('/', '-')}"
            ])
        }
        // archive Confiperf results
        archiveResults(settings.archive, pattern, settings.allowEmptyResults)
    }
}

void archiveResults(archive, pattern, allowEmpty) {
    if (archive) {
        log("archive ${pattern}")
        archiveArtifacts artifacts: pattern, allowEmptyArchive: allowEmpty
    }
}

void log(msg) {
    echo "[${STEP_NAME}] ${msg}"
}

void touchFiles(pattern){
    log('update test results')
    def patternArray = pattern.split(',')
    for(def i = 0; i < patternArray.length; i++){
        sh "find . -wholename '${patternArray[i].trim()}' -exec touch {} \\;"
    }
}

def prepare(parameters){
    // ensure tool maps are initialized correctly
    for(String tool : TOOLS){
        parameters[tool] = toMap(parameters[tool])
    }
    return parameters
}

def toMap(parameters){
    if(MapUtils.isMap(parameters)){
        parameters.put('active', parameters.active == null?true:parameters.active)
    }else if(Boolean.TRUE.equals(parameters)){
        parameters = [active: true]
    }else if(Boolean.FALSE.equals(parameters)){
        parameters = [active: false]
    }else {
        parameters = [:]
    }
    return parameters
}
